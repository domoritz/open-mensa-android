buildscript {
    ext.kotlin_version = '1.9.21'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.8.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.google.devtools.ksp:com.google.devtools.ksp.gradle.plugin:1.9.21-1.0.16"
    }
}

apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'com.google.devtools.ksp'

repositories {
    google()
    mavenCentral()
}

dependencies {
    def room_version = "2.6.1"

    implementation platform('androidx.compose:compose-bom:2025.02.00')
    implementation 'androidx.activity:activity-compose:1.10.0'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation('androidx.compose.material:material-icons-extended:1.7.8')

    // compile fileTree(dir: 'libs', include: '*.jar')
    implementation "com.google.android.material:material:1.12.0"
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.work:work-runtime-ktx:2.10.0'

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    ksp "androidx.room:room-compiler:$room_version"
}

android {
    namespace 'de.uni_potsdam.hpi.openmensa'

    defaultConfig {
        applicationId 'de.uni_potsdam.hpi.openmensa'
        versionCode 33
        versionName "2.0.1"
        minSdkVersion 21
        targetSdkVersion 35
    }

    compileSdk 35

    dataBinding {
        enabled = true
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    ksp {
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        androidTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_21
        targetCompatibility JavaVersion.VERSION_21
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.7"
    }
}